## BaseMenuController.gd
## Base class for menu controllers.
## Owns all navigation, focus memory, and device-aware focus logic.
##
## Usage:
##   extends BaseMenuController
##
##   enum State { MAIN, OPTIONS, CREDITS }
##
##   @onready var panel_main:    BaseMenu = $MainPanel
##   @onready var panel_options: BaseMenu = $OptionsPanel
##   @onready var panel_credits: BaseMenu = $CreditsPanel
##
##   func _ready() -> void:
##       _panels = {
##           State.MAIN:    panel_main,
##           State.OPTIONS: panel_options,
##           State.CREDITS: panel_credits,
##       }
##       _initial_state = State.MAIN
##
##       panel_main.options_requested.connect(go_to.bind(State.OPTIONS))
##       panel_options.back_requested.connect(go_back)
##
##       super._ready()  ## always last

class_name BaseMenuController
extends Control

signal close_requested

# To set before super._ready()
## Maps State enum values (int) to BaseMenu nodes.
## Populate in _ready() before calling super._ready().
var _panels: Dictionary = {}

## The state to activate on startup.
## Set in _ready() before calling super._ready().
var _initial_state: int = 0


# Private state
var _current: int = -1
var _history: Array[int] = []

## Stores the last focused Control per state, for restoration on go_back().
var _focus_memory: Dictionary = {}  # int -> Control


func _ready() -> void:
	DeviceManager.method_changed.connect(_on_method_changed)
	go_to(_initial_state)


func _exit_tree() -> void:
	DeviceManager.method_changed.disconnect(_on_method_changed)


# Public API

## activates the current panel and shows the controller:
func open() -> void:
	show()
	_panels[_current].activate()
	_grab_focus(_current)


## deactivates the current panel, hides the controller, emits the signal:
func close() -> void:
	if _current != -1:
		_panels[_current].deactivate()
	_history.clear()
	_current = _initial_state
	hide()
	close_requested.emit()



## Navigate to a new state, pushing the current one onto the history stack.
func go_to(state: int) -> void:
	var previous := _current
	_save_focus(previous)
	
	assert(_panels.has(state), "BaseMenuController: no panel registered for state %d" % state)
	_current = state
	_panels[_current].activate()
	
	_grab_focus(_current)
	if previous != -1:
		_panels[previous].deactivate()
		_history.push_back(previous)


## Return to the previous state. Does nothing if history is empty.
func go_back() -> void:
	if _history.is_empty():
		close()
		return
	
	var previous := _current
	_save_focus(previous)
	_current = _history.pop_back()
	_panels[_current].activate()
	_grab_focus(_current)
	_panels[previous].deactivate()


# Internal

func _grab_focus(state: int) -> void:
	if DeviceManager.last_input_method == DeviceManager.InputMethod.MOUSE or DeviceManager.last_input_method == DeviceManager.InputMethod.TOUCH:
		return
	
	var remembered = _focus_memory.get(state, null)
	if is_instance_valid(remembered) and remembered is Control:
		remembered.grab_focus()
	else:
		_panels[state].grab_default_focus()


func _save_focus(state: int) -> void:
	if state == -1:
		return
	var focused := get_viewport().gui_get_focus_owner()
	if is_instance_valid(focused):
		_focus_memory[state] = focused


func _on_method_changed(method: DeviceManager.InputMethod) -> void:
	match method:
		DeviceManager.InputMethod.GAMEPAD, DeviceManager.InputMethod.KEYBOARD:
			if _current != -1:
				_grab_focus(_current)
		DeviceManager.InputMethod.TOUCH, DeviceManager.InputMethod.MOUSE:
			var focused := get_viewport().gui_get_focus_owner()
			if is_instance_valid(focused) and not (focused is TextEdit or focused is LineEdit):
				focused.release_focus()
